##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;import lux
         (lux (control (monoid #as m)
                       (eq #as E)
                       (show #as S)
                       (read #as R))
              (codata function)))

## [Structures]
## Eq
(defstruct #export Bool/Eq (E;Eq Bool)
  (def (= x y)
    (if x
      y
      (not y))))

## Monoid
(do-template [<name> <unit> <op>]
  [(defstruct #export <name> (m;Monoid Bool)
     (def unit <unit>)
     (def (++ x y)
       (<op> x y)))]

  [ Or/Monoid false or]
  [And/Monoid true and]
  )

## Show
(defstruct #export Bool/Show (S;Show Bool)
  (def (show x)
    (if x
      "true"
      "false")))

## Read
(defstruct #export Bool/Read (R;Read Bool)
  (def (read input)
    (case input
      "true"  (#;Some true)
      "false" (#;Some false)
      _       #;None)))

## [Functions]
(def #export comp
  (All [a] (-> (-> a Bool) (-> a Bool)))
  (. not))
