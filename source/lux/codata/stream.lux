##  Copyright (c) Eduardo Julian. All rights reserved.
##  This Source Code Form is subject to the terms of the Mozilla Public License, v. 2.0.
##  If a copy of the MPL was not distributed with this file,
##  You can obtain one at http://mozilla.org/MPL/2.0/.

(;import lux
         (lux (control (functor #as F #refer #all)
                       (monad #as M #refer #all)
                       (comonad #as CM #refer #all))
              (meta lux
                    syntax)
              (data (list #as l #refer (#only @list @list& List/Monad) #open ("" List/Fold))
                    (number (int #open ("i" Int/Number Int/Ord)))
                    bool)
              (codata (lazy #as L #refer #all))))

(open List/Monad "list:")

## [Types]
(deftype #export (Stream a)
  (Lazy (, a (Stream a))))

## [Utils]
(def (cycle' x xs init full)
  (All [a]
    (-> a (List a) a (List a) (Stream a)))
  (case xs
    #;Nil           (cycle' init full init full)
    (#;Cons x' xs') (... [x (cycle' x' xs' init full)])))

## [Functions]
(def #export (iterate f x)
  (All [a]
    (-> (-> a a) a (Stream a)))
  (... [x (iterate f (f x))]))

(def #export (repeat x)
  (All [a]
    (-> a (Stream a)))
  (... [x (repeat x)]))

(def #export (cycle xs)
  (All [a]
    (-> (List a) (Maybe (Stream a))))
  (case xs
    #;Nil          #;None
    (#;Cons x xs') (#;Some (cycle' x xs' x xs'))))

(do-template [<name> <return> <part>]
  [(def #export (<name> s)
     (All [a] (-> (Stream a) <return>))
     (let [[h t] (! s)]
       <part>))]

  [head a          h]
  [tail (Stream a) t])

(def #export (@ idx s)
  (All [a] (-> Int (Stream a) a))
  (let [[h t] (! s)]
    (if (i> idx 0)
      (@ (i+ -1 idx) t)
      h)))

(do-template [<taker> <dropper> <splitter> <det-type> <det-test> <det-step>]
  [(def #export (<taker> det xs)
     (All [a]
       (-> <det-type> (Stream a) (List a)))
     (let [[x xs'] (! xs)]
       (if <det-test>
         (@list& x (<taker> <det-step> xs'))
         (@list))))

   (def #export (<dropper> det xs)
     (All [a]
       (-> <det-type> (Stream a) (Stream a)))
     (let [[x xs'] (! xs)]
       (if <det-test>
         (<dropper> <det-step> xs')
         xs)))

   (def #export (<splitter> det xs)
     (All [a]
       (-> <det-type> (Stream a) (, (List a) (Stream a))))
     (let [[x xs'] (! xs)]
       (if <det-test>
         (let [[tail next] (<splitter> <det-step> xs')]
           [(#;Cons [x tail]) next])
         [(@list) xs])))]
  
  [take-while drop-while split-with (-> a Bool) (det x)    det]
  [take       drop       split      Int         (i> det 0) (i+ -1 det)]
  )

(def #export (unfold step init)
  (All [a b]
    (-> (-> a (, a b)) a (Stream b)))
  (let [[next x] (step init)]
    (... [x (unfold step next)])))

(def #export (filter p xs)
  (All [a] (-> (-> a Bool) (Stream a) (Stream a)))
  (let [[x xs'] (! xs)]
    (if (p x)
      (... [x (filter p xs')])
      (filter p xs'))))

(def #export (partition p xs)
  (All [a] (-> (-> a Bool) (Stream a) (, (Stream a) (Stream a))))
  [(filter p xs) (filter (comp p) xs)])

## [Structures]
(defstruct #export Stream/Functor (Functor Stream)
  (def (map f fa)
    (let [[h t] (! fa)]
      (... [(f h) (map f t)]))))

(defstruct #export Stream/CoMonad (CoMonad Stream)
  (def _functor Stream/Functor)
  (def unwrap head)
  (def (split wa)
    (let [[head tail] (! wa)]
      (... [wa (split tail)]))))

## [Pattern-matching]
(defsyntax #export (\stream& body [patterns (+^ id^)])
  (case (l;reverse patterns)
    (\ (@list& last prevs))
    (do Lux/Monad
      [prevs (map% Lux/Monad macro-expand-1 prevs)
       g!s (gensym "s")
       #let [body+ (foldL (lambda [inner outer]
                            (` (let [[(~ outer) (~ g!s)] (! (~ g!s))]
                                 (~ inner))))
                          (` (let [(~ last) (~ g!s)] (~ body)))
                          prevs)]]
      (wrap (@list g!s body+)))

    _
    (fail "Wrong syntax for \\stream&")))
